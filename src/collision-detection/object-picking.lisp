(in-package #:net.mfiano.lisp.pyx)

(defclass picker ()
  ((%start :reader start
           :initform (v3:vec))
   (%end :reader end
         :initform (v3:vec))))

(defun make-picker ()
  (make-instance 'picker))

(defgeneric pick-collider-shape (picker shape)
  (:method (picker shape)))

(defmethod pick-collider-shape (picker (shape collider-shape/sphere))
  (with-slots (%entity %center %radius) shape
    (let* ((line (v3:- (end picker) (start picker)))
           (d (v3:normalize line))
           (m (v3:- (start picker) (transform-point %entity %center)))
           (b (v3:dot m d))
           (c (- (v3:dot m m) (expt %radius 2))))
      (unless (and (plusp c) (plusp b))
        (let ((discriminant (- (expt b 2) c)))
          (unless (minusp discriminant)
            (let ((x (max 0.0 (- (- b) (sqrt discriminant)))))
              (when (<= x (v3:length line))
                x))))))))

(defun update-picker ()
  (u:mvlet ((x y dx dy (get-mouse-position)))
    (u:when-let* ((viewport (get-viewport-by-coordinates x y))
                  (picker (picker viewport))
                  (camera (camera viewport))
                  (view (camera/view camera))
                  (proj (camera/projection camera))
                  (viewport (v4:vec (x viewport)
                                    (y viewport)
                                    (width viewport)
                                    (height viewport))))
      (math:unproject! (start picker) (v3:vec x y) view proj viewport)
      (math:unproject! (end picker) (v3:vec x y 1) view proj viewport)
      (values (start picker) (end picker)))))

(defun pick-entity ()
  (let* ((viewport (active (get-viewport-manager)))
         (picker (picker viewport))
         (picked nil))
    (update-picker)
    (u:do-hash-values (v (active (collision-system (current-scene))))
      (u:do-hash-keys (k v)
        (u:when-let ((n (pick-collider-shape picker (collider/shape k))))
          (push (cons n k) picked))))
    (when picked
      (let* ((collider (cdar (stable-sort picked #'< :key #'car)))
             (entity (collider/owner collider)))
        (setf (picked-entity (current-scene)) entity)
        (on-collision-picked (collider/layer collider) entity)))))

(defun entity-picked-p (entity)
  (eq entity (picked-entity (current-scene))))
