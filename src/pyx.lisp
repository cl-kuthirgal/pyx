(in-package #:net.mfiano.lisp.pyx)

(defun initialize (user-args)
  (setup-repl)
  (load-config)
  (initialize-rng)
  (prepare-gamepads)
  (make-display)
  (make-input-data)
  (load-hardware-info)
  (make-thread-pool)
  (initialize-shaders)
  (make-clock)
  (apply #'on-context-create =context= user-args)
  (start-loop))

(defun deinitialize ()
  (unwind-protect
       (progn
         (on-context-destroy =context=)
         (shutdown-gamepads)
         (kill-display)
         (destroy-thread-pool)
         (reset-config)
         (sdl2:quit))
    (setf =context= nil)))

(defun process-end-frame-work ()
  (map nil #'funcall (nreverse (end-frame-work)))
  (setf (end-frame-work) nil))

(defun update ()
  (let ((alpha (get-alpha)))
    (do-nodes (node)
      (on-update node))
    (process-end-frame-work)
    (do-nodes (node)
      (resolve-model node alpha))))

(defun physics-update ()
  (do-nodes (node)
    (on-physics-update node)
    (transform-node node))
  (compute-collisions))

(defun periodic-update ()
  #-pyx.release (update-repl)
  (process-queue :recompile))

(defun start-loop ()
  (let* ((clock (clock))
         (display (display))
         (input-data (input-data))
         (refresh-rate (refresh-rate display)))
    (update)
    (u:while (running-p)
      (with-continuable "Pyx"
        (handle-events input-data)
        (tick-clock clock refresh-rate #'physics-update #'periodic-update)
        (update)
        (render display)))))

(defun start-engine (context-name &rest user-args)
  (unless (and =context= (running-p))
    (setf =context= (make-context context-name))
    (unwind-protect (initialize user-args)
      (deinitialize))))

(defun stop-engine ()
  (setf (running-p) nil))
